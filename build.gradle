plugins {
    id 'application'
    id 'java'
    id 'jacoco'
}

group 'uk.ashleybye'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven { url "https://raw.githubusercontent.com/AshleyByeUK/maven-repo/master/" }
    mavenCentral()
}

dependencies {
    compile "uk.ashleybye.tictactoe:tictactoe-core:1.0"
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

application {
    mainClassName = 'uk.ashleybye.tictactoe.Application'
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'uk.ashleybye.tictactoe.Application'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDir = file("$buildDir")
}

wrapper {
    gradleVersion = '4.10.2'
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
    }
    jvmArgs "-Dheadless=true"
}

task testSlow(type: Test) {
    useJUnitPlatform()
    jvmArgs "-Dheadless=true"
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules.rule {
        limit { counter = 'INSTRUCTION'; minimum = 0.90 }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
